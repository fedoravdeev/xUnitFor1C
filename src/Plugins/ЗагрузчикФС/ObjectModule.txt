Перем Ядро;

Процедура УстановитьЯдро(НовоеЯдро) Экспорт
	Ядро = НовоеЯдро;
КонецПроцедуры

Функция ПолучитьЯдро() Экспорт
	Возврат Ядро;
КонецФункции

Функция ОписаниеПлагина() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", Ядро.ТипыПлагинов().Загрузчик);
	Результат.Вставить("Идентификатор", "ЗагрузчикФС");
	Результат.Вставить("Представление", "Загрузить тесты из файловой системы");
	
	Возврат Результат;
КонецФункции

Процедура УстановитьПуть(Путь) Экспорт
	ЭтотОбъект.Путь = Путь;
КонецПроцедуры

Функция ПолучитьПуть() Экспорт
	Возврат Путь;
КонецФункции

Функция ВыбратьПутьИнтерактивно() Экспорт
	ВызватьИсключение "Не реализовано";
КонецФункции

Функция Загрузить() Экспорт
	МойПуть = ПолучитьПуть();
	ФайлОбработки = Новый Файл(МойПуть);
	Если Не ФайлОбработки.Существует() Тогда
		ВызватьИсключение "Не существует файл тестов для загрузки <" + МойПуть + ">";
	КонецЕсли;
	Если ФайлОбработки.ЭтоКаталог() Тогда
		ВызватьИсключение "Для загрузки передан каталог вместо файла <" + МойПуть + ">";
	КонецЕсли;
	
	ДеревоТестов = Ядро.НовоеДеревоТестов();
	Попытка
		Обработка = ВнешниеОбработки.Создать(ФайлОбработки.ПолноеИмя);
		СписокТестов = Обработка.ПолучитьСписокТестов(Ядро);
	Исключение
		// TODO добавить обработку ошибок
		Описание = ОписаниеОшибки();
		Сообщить(Описание);
		Возврат ДеревоТестов;
	КонецПопытки;
	Контейнер = Ядро.ДобавитьКонтейнерВДеревоТестов(ДеревоТестов, ФайлОбработки.ИмяБезРасширения);
	Для каждого ИмяТеста Из СписокТестов Цикл
		Ядро.ДобавитьЭлементВДеревоТестов(Контейнер, ФайлОбработки.ПолноеИмя, ИмяТеста);
	КонецЦикла;
	
	Возврат ДеревоТестов;
КонецФункции