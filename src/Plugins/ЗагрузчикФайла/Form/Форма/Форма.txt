
&НаКлиенте
Функция ОписаниеПлагина(ВозможныеТипыПлагинов) Экспорт
	Возврат ОписаниеПлагина_Сервер(ВозможныеТипыПлагинов);
КонецФункции

Функция ОписаниеПлагина_Сервер(ВозможныеТипыПлагинов)
	ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ЭтотОбъект.ОписаниеПлагина(ВозможныеТипыПлагинов);
КонецФункции

&НаКлиенте
Функция ВыбратьПутьИнтерактивно(ТекущийПуть = "") Экспорт
	ДиалогВыбораТеста = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораТеста.Фильтр = "Обработка-тест (*.epf)|*.epf|Все файлы|*";
	ДиалогВыбораТеста.МножественныйВыбор = Истина;
	ДиалогВыбораТеста.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораТеста.ПолноеИмяФайла = ТекущийПуть;
	
	Результат = Новый ТекстовыйДокумент;
	Если ДиалогВыбораТеста.Выбрать() Тогда
		Для каждого ПолноеИмяФайла Из ДиалогВыбораТеста.ВыбранныеФайлы Цикл
			Результат.ДобавитьСтроку(ПолноеИмяФайла);
		КонецЦикла;		
	КонецЕсли;
	Текст = Результат.ПолучитьТекст();
	
	Возврат Лев(Текст, СтрДлина(Текст) - 1);
КонецФункции

&НаКлиенте
Функция Загрузить(КонтекстЯдра, Путь) Экспорт
	ДеревоТестов = КонтекстЯдра.НовоеДеревоТестов();
	Для Сч = 1 По СтрЧислоСтрок(Путь) Цикл
		ФайлОбработки = Новый Файл(СтрПолучитьСтроку(Путь, Сч));
		Если Не ФайлОбработки.Существует() Тогда
			ВызватьИсключение "Не существует файл тестов для загрузки <" + ФайлОбработки.ПолноеИмя + ">";
		КонецЕсли;
		Если ФайлОбработки.ЭтоКаталог() Тогда
			ВызватьИсключение "Для загрузки передан каталог вместо файла <" + ФайлОбработки.ПолноеИмя + ">";
		КонецЕсли;
		
		АдресХранилища = "";
		ПоместитьФайл(АдресХранилища, ФайлОбработки.ПолноеИмя, , Ложь, ЭтаФорма.УникальныйИдентификатор);
		ПодключитьФайлОбработкиНаСервере(АдресХранилища, ФайлОбработки.ИмяБезРасширения);
		ЗагрузитьФайл_Клиент(КонтекстЯдра, ДеревоТестов, ФайлОбработки);
		ЗагрузитьФайл_Сервер(ДеревоТестов, ФайлОбработки.ИмяБезРасширения, ФайлОбработки.ПолноеИмя);
	КонецЦикла;
	
	Возврат ДеревоТестов;
КонецФункции

&НаСервереБезКонтекста
Процедура ПодключитьФайлОбработкиНаСервере(АдресХранилища, ИдентификаторОбработки)
	ВнешниеОбработки.Подключить(АдресХранилища, ИдентификаторОбработки, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл_Клиент(КонтекстЯдра, ДеревоТестов, ФайлОбработки)
	ФормаОбработки = ПолучитьФорму("ВнешняяОбработка." + ФайлОбработки.ИмяБезРасширения + ".Форма");
	Попытка
		СписокТестов = ФормаОбработки.ПолучитьСписокТестов(КонтекстЯдра);
		Контейнер = КонтекстЯдра.ДобавитьКонтейнерВДеревоТестов(ДеревоТестов, ФайлОбработки.ИмяБезРасширения);
		Для каждого ИмяТеста Из СписокТестов Цикл
			КонтекстЯдра.ДобавитьЭлементВКонтейнер(Контейнер, ФайлОбработки.ПолноеИмя, ИмяТеста);
		КонецЦикла;
	Исключение
		// TODO добавить обработку ошибок
		Описание = ОписаниеОшибки();
		Сообщить(Описание);
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФайл_Сервер(ДеревоТестов, ИдентификаторОбработки, ПолныйПутьКОбработкеНаКлиенте)
	КонтекстЯдра = ВнешниеОбработки.Создать("xddTestRunner");
	Попытка
		Обработка = ВнешниеОбработки.Создать(ИдентификаторОбработки);
		СписокТестов = Обработка.ПолучитьСписокТестов(КонтекстЯдра);
		Контейнер = КонтекстЯдра.ДобавитьКонтейнерВДеревоТестов(ДеревоТестов, ИдентификаторОбработки);
		Для каждого ИмяТеста Из СписокТестов Цикл
			КонтекстЯдра.ДобавитьЭлементВКонтейнер(Контейнер, ПолныйПутьКОбработкеНаКлиенте, ИмяТеста);
		КонецЦикла;
	Исключение
		// TODO добавить обработку ошибок
		Описание = ОписаниеОшибки();
		Сообщить(Описание);
	КонецПопытки;
КонецПроцедуры
