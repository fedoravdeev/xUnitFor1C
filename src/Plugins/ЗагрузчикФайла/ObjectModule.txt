Перем Ядро;

Процедура УстановитьЯдро(НовоеЯдро) Экспорт
	Ядро = НовоеЯдро;
КонецПроцедуры

Функция ПолучитьЯдро() Экспорт
	Возврат Ядро;
КонецФункции

Функция ОписаниеПлагина() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", Ядро.ТипыПлагинов().Загрузчик);
	Результат.Вставить("Идентификатор", "ЗагрузчикФайла");
	Результат.Вставить("Представление", "Загрузить тесты из файловой системы");
	
	Возврат Результат;
КонецФункции

Процедура УстановитьПуть(Путь) Экспорт
	ЭтотОбъект.Путь = Путь;
КонецПроцедуры

Функция ПолучитьПуть() Экспорт
	Возврат Путь;
КонецФункции

#Если ТолстыйКлиентОбычноеПриложение Тогда
Функция ВыбратьПутьИнтерактивно() Экспорт
	ДиалогВыбораТеста = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораТеста.Фильтр = "Обработка-тест (*.epf)|*.epf|Все файлы|*";
	ДиалогВыбораТеста.МножественныйВыбор = Истина;
	ДиалогВыбораТеста.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораТеста.Каталог = ПолучитьПуть();
	
	Результат = Новый ТекстовыйДокумент;
	Если ДиалогВыбораТеста.Выбрать() Тогда
		Для каждого ПолноеИмяФайла Из ДиалогВыбораТеста.ВыбранныеФайлы Цикл
			Результат.ДобавитьСтроку(ПолноеИмяФайла);
		КонецЦикла;		
	КонецЕсли;
	Текст = Результат.ПолучитьТекст();
	
	Возврат Лев(Текст, СтрДлина(Текст) - 1);
КонецФункции
#КонецЕсли

Функция Загрузить() Экспорт
	ДеревоТестов = Ядро.НовоеДеревоТестов();
	СтрокаПутей = ПолучитьПуть();
	Для Сч = 1 По СтрЧислоСтрок(СтрокаПутей) Цикл
		ФайлОбработки = Новый Файл(СтрПолучитьСтроку(СтрокаПутей, Сч));
		ЗагрузитьФайл(ФайлОбработки, ДеревоТестов);
	КонецЦикла; 
	
	Возврат ДеревоТестов;
КонецФункции

Функция ЗагрузитьФайл(ФайлОбработки, ДеревоТестов)
	Если Не ФайлОбработки.Существует() Тогда
		ВызватьИсключение "Не существует файл тестов для загрузки <" + ФайлОбработки.ПолноеИмя + ">";
	КонецЕсли;
	Если ФайлОбработки.ЭтоКаталог() Тогда
		ВызватьИсключение "Для загрузки передан каталог вместо файла <" + ФайлОбработки.ПолноеИмя + ">";
	КонецЕсли;
	
	Попытка
		Обработка = ВнешниеОбработки.Создать(ФайлОбработки.ПолноеИмя);
		СписокТестов = Обработка.ПолучитьСписокТестов(Ядро);
	Исключение
		// TODO добавить обработку ошибок
		Описание = ОписаниеОшибки();
		Сообщить(Описание);
		Возврат ДеревоТестов;
	КонецПопытки;
	Контейнер = Ядро.ДобавитьКонтейнерВДеревоТестов(ДеревоТестов, ФайлОбработки.ИмяБезРасширения);
	Для каждого ИмяТеста Из СписокТестов Цикл
		Ядро.ДобавитьЭлементВДеревоТестов(Контейнер, ФайлОбработки.ПолноеИмя, ИмяТеста);
	КонецЦикла;
	
	Возврат Контейнер;
КонецФункции