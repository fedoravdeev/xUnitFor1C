
Функция ОписаниеПлагина(ВозможныеТипыПлагинов) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Тип", ВозможныеТипыПлагинов.Загрузчик);
	Результат.Вставить("Идентификатор", "ЗагрузчикФайла");
	Результат.Вставить("Представление", "Загрузить тесты из файловой системы");
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
КонецФункции

#Если ТолстыйКлиентОбычноеПриложение Тогда
Функция ВыбратьПутьИнтерактивно(ТекущийПуть = "") Экспорт
	ДиалогВыбораТеста = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораТеста.Фильтр = "Обработка-тест (*.epf)|*.epf|Все файлы|*";
	ДиалогВыбораТеста.МножественныйВыбор = Истина;
	ДиалогВыбораТеста.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораТеста.ПолноеИмяФайла = ТекущийПуть;
	
	Результат = Новый ТекстовыйДокумент;
	Если ДиалогВыбораТеста.Выбрать() Тогда
		Для каждого ПолноеИмяФайла Из ДиалогВыбораТеста.ВыбранныеФайлы Цикл
			Результат.ДобавитьСтроку(ПолноеИмяФайла);
		КонецЦикла;		
	КонецЕсли;
	Текст = Результат.ПолучитьТекст();
	
	Возврат Лев(Текст, СтрДлина(Текст) - 1);
КонецФункции
#КонецЕсли

Функция Загрузить(КонтекстЯдра, Путь) Экспорт
	ДеревоТестов = Неопределено;
	Для Сч = 1 По СтрЧислоСтрок(Путь) Цикл
		ФайлОбработки = Новый Файл(СтрПолучитьСтроку(Путь, Сч));
		ЗагрузитьФайл(КонтекстЯдра, ДеревоТестов, ФайлОбработки);
	КонецЦикла;
	
	Возврат ДеревоТестов;
КонецФункции

Процедура ЗагрузитьФайл(КонтекстЯдра, ДеревоТестов, ФайлОбработки)
	Если Не ФайлОбработки.Существует() Тогда
		ВызватьИсключение "Не существует файл тестов для загрузки <" + ФайлОбработки.ПолноеИмя + ">";
	КонецЕсли;
	Если ФайлОбработки.ЭтоКаталог() Тогда
		ВызватьИсключение "Для загрузки передан каталог вместо файла <" + ФайлОбработки.ПолноеИмя + ">";
	КонецЕсли;
	
	Если ДеревоТестов = Неопределено Тогда
		ДеревоТестов = КонтекстЯдра.НовоеДеревоТестов(ФайлОбработки.Путь);
	КонецЕсли;
	
	Попытка
		Обработка = ВнешниеОбработки.Создать(ФайлОбработки.ПолноеИмя);
		СписокТестов = Обработка.ПолучитьСписокТестов(КонтекстЯдра);
		Контейнер = КонтекстЯдра.ДобавитьКонтейнерВДеревоТестов(ДеревоТестов, ФайлОбработки.ИмяБезРасширения);
		Для каждого ИмяТеста Из СписокТестов Цикл
			КонтекстЯдра.ДобавитьЭлементВКонтейнер(Контейнер, ФайлОбработки.ПолноеИмя, ИмяТеста);
		КонецЦикла;
	Исключение
		// TODO добавить обработку ошибок
		Описание = ОписаниеОшибки();
		Сообщить(Описание);
	КонецПопытки;
КонецПроцедуры
