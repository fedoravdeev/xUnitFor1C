Перем юТест;
Перем Утверждения;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	юТест = ЮнитТестирование;
	Утверждения = юТест;
	
	ВсеТесты = Новый Массив;
	// Core
	ВсеТесты.Добавить("ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу");
	// Plugin interface
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОписаниеПлагина");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

// { Core
Процедура ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору() Экспорт
	ЗагрузчикТестов = юТест.Плагин("ЗагрузчикФайла");
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры

Процедура ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу() Экспорт
	Загрузчики = юТест.ПолучитьИдентификаторыПлагиновПоТипу(юТест.ТипыПлагинов().Загрузчик);
	
	Утверждения.ПроверитьТип(Загрузчики, "Массив", "Загрузчики");
	Утверждения.ПроверитьБольше(Загрузчики.Количество(), 0, "Загрузчики.Количество()");
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	Для каждого ИдентификаторЗагрузчика Из Загрузчики Цикл
		ЗагрузчикТестов = юТест.Плагин(ИдентификаторЗагрузчика);
		ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
		Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Идентификатор, "ОписаниеПлагина.Идентификатор");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Представление, "ОписаниеПлагина.Представление");
	КонецЦикла;
	
	ЗагрузчикПоУмолчанию = юТест.ЗагрузчикПоУмолчанию();
	ИндексЗагрузчика = Загрузчики.Найти(ЗагрузчикПоУмолчанию.ОписаниеПлагина(ВозможныеТипыПлагинов).Идентификатор);
	Утверждения.ПроверитьНеРавенство(ИндексЗагрузчика, Неопределено, "ИндексЗагрузчика");
КонецПроцедуры
// } Core

// { Plugin interface
Процедура ТестДолжен_ПроверитьОписаниеПлагина() Экспорт
	ЗагрузчикТестов = юТест.ЗагрузчикПоУмолчанию();
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры
// } Plugin interface