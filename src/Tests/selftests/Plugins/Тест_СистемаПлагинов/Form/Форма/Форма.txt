&НаКлиенте
Перем юТест;
&НаКлиенте
Перем Утверждения;

&НаКлиенте
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	юТест = ЮнитТестирование;
	Утверждения = юТест;
	
	ВсеТесты = Новый Массив;
	
	// Plugin interface
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОписаниеПлагина");
	
	// Core
	ВсеТесты.Добавить("ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу");
	
	Возврат ВсеТесты;
КонецФункции

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

// { Plugin interface
&НаКлиенте
Процедура ТестДолжен_ПроверитьОписаниеПлагина() Экспорт
	ЗагрузчикТестов = юТест.ЗагрузчикПоУмолчанию();
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры
// } Plugin interface

// { Core
&НаКлиенте
Процедура ТестДолжен_ПроверитьПолучениеПлагинаПоИдентификатору() Экспорт
	ЗагрузчикТестов = юТест.Плагин("ЗагрузчикФайла");
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Идентификатор, "ЗагрузчикФайла", "ОписаниеПлагина.Идентификатор");
	Утверждения.ПроверитьРавенство(ОписаниеПлагина.Представление, "Загрузить тесты из файловой системы", "ОписаниеПлагина.Представление");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПолучитьСписокЗагруженныхПлагиновПоТипу() Экспорт
	ВозможныеТипыПлагинов = юТест.ТипыПлагинов();
	Загрузчики = юТест.ПолучитьИдентификаторыПлагиновПоТипу(ВозможныеТипыПлагинов.Загрузчик);
	
	Утверждения.ПроверитьТип(Загрузчики, "Массив", "Загрузчики");
	Утверждения.ПроверитьБольше(Загрузчики.Количество(), 0, "Загрузчики.Количество()");
	Для каждого ИдентификаторЗагрузчика Из Загрузчики Цикл
		ЗагрузчикТестов = юТест.Плагин(ИдентификаторЗагрузчика);
		ОписаниеПлагина = ЗагрузчикТестов.ОписаниеПлагина(ВозможныеТипыПлагинов);
		Утверждения.ПроверитьРавенство(ОписаниеПлагина.Тип, ВозможныеТипыПлагинов.Загрузчик, "ОписаниеПлагина.Тип, юТест.ТипыПлагинов().Загрузчик");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Идентификатор, "ОписаниеПлагина.Идентификатор");
		Утверждения.ПроверитьЗаполненность(ОписаниеПлагина.Представление, "ОписаниеПлагина.Представление");
	КонецЦикла;
	
	ЗагрузчикПоУмолчанию = юТест.ЗагрузчикПоУмолчанию();
	ИндексЗагрузчика = Загрузчики.Найти(ЗагрузчикПоУмолчанию.ОписаниеПлагина(ВозможныеТипыПлагинов).Идентификатор);
	Утверждения.ПроверитьНеРавенство(ИндексЗагрузчика, Неопределено, "ИндексЗагрузчика");
КонецПроцедуры
// } Core
