Перем юТест;
Перем Утверждения;
Перем ЗагрузчикФайла;
Перем ВременныеФайлы;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	юТест = ЮнитТестирование;
	Утверждения = юТест;
	
	ВсеТесты = Новый Массив;
	// Loader interface
	ВсеТесты.Добавить("ТестДолжен_УстановитьПуть_ПолучитьПуть");
	ВсеТесты.Добавить("ТестДолжен_Загрузить_ОдинФайл");
	ВсеТесты.Добавить("ТестДолжен_Загрузить_ДваФайла");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	ВременныеФайлы = Новый Массив;
	ЗагрузчикФайла = юТест.Плагин("ЗагрузчикФайла");
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	ЗагрузчикФайла = Неопределено;
	УдалитьВременныеФайлы();
КонецПроцедуры

// { Helpers
Функция НовыйВременныйФайл(Расширение) Экспорт
	ВременныйФайл = Новый Файл(ПолучитьИмяВременногоФайла(Расширение));
	ВременныеФайлы.Добавить(ВременныйФайл);
	
	Возврат ВременныйФайл;
КонецФункции 

Процедура УдалитьВременныеФайлы()
	Для каждого ВременныйФайл Из ВременныеФайлы Цикл
		Попытка
			УдалитьФайлы(ВременныйФайл.ПолноеИмя);
		Исключение
			Сообщить("Не удален временный файл: " + ВременныйФайл.ПолноеИмя + "
			|-" + ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;
	ВременныеФайлы.Очистить();
КонецПроцедуры
// } Helpers

// { Loader interface
Процедура ТестДолжен_УстановитьПуть_ПолучитьПуть() Экспорт
	ТестовыйПуть = "c:\windows\";
	ЗагрузчикФайла.УстановитьПуть(ТестовыйПуть);
	
	Путь = ЗагрузчикФайла.ПолучитьПуть();
	
	Утверждения.ПроверитьРавенство(Путь, ТестовыйПуть);
КонецПроцедуры

Процедура ТестДолжен_Загрузить_ОдинФайл() Экспорт
	ФайлСТестами = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами.ПолноеИмя);
	
	ЗагрузчикФайла.УстановитьПуть(ФайлСТестами.ПолноеИмя);
	ДеревоТестов = ЗагрузчикФайла.Загрузить();
	
	Утверждения.ПроверитьТип(ДеревоТестов, "Массив", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Количество(), 1, "Узлы верхнего уровня");
	Контейнер = ДеревоТестов[0];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами);
КонецПроцедуры

Процедура ПроверитьКонтейнер(Контейнер, ФайлСТестами)
	Утверждения.ПроверитьРавенство(Контейнер.Тип, юТест.ТипыУзловДереваТестов().Контейнер, "Контейнер.Тип");
	Утверждения.ПроверитьРавенство(Контейнер.Имя, ФайлСТестами.ИмяБезРасширения, "Контейнер.Имя");
	Утверждения.ПроверитьТип(Контейнер.Строки, "Массив", "Контейнер.Строки");
	Утверждения.ПроверитьРавенство(Контейнер.Строки.Количество(), 3, "Контейнер.Строки.Количество()");
	
	Элемент1 = Контейнер.Строки[0];
	Утверждения.ПроверитьРавенство(Элемент1.Тип, юТест.ТипыУзловДереваТестов().Элемент, "Элемент1.Тип");
	Утверждения.ПроверитьРавенство(Элемент1.Путь, ФайлСТестами.ПолноеИмя, "Элемент1.Путь");
	Утверждения.ПроверитьРавенство(Элемент1.ИмяТеста, "УспешныйТест", "Элемент1.ИмяТеста");
	
	Элемент3 = Контейнер.Строки[2];
	Утверждения.ПроверитьРавенство(Элемент3.Тип, юТест.ТипыУзловДереваТестов().Элемент, "Элемент3.Тип");
	Утверждения.ПроверитьРавенство(Элемент3.Путь, ФайлСТестами.ПолноеИмя, "Элемент3.Путь");
	Утверждения.ПроверитьРавенство(Элемент3.ИмяТеста, "НесуществующийТест", "Элемент3.ИмяТеста");
КонецПроцедуры

Процедура ТестДолжен_Загрузить_ДваФайла() Экспорт
	ФайлСТестами1 = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами1.ПолноеИмя);
	
	ФайлСТестами2 = НовыйВременныйФайл(".epf");
	ЭтотОбъект.ПолучитьМакет("ТестовыйНаборДляЗагрузчикаФС").Записать(ФайлСТестами2.ПолноеИмя);
	
	ПутьКНесколькимФайлам = ФайлСТестами1.ПолноеИмя + Символы.ПС + ФайлСТестами2.ПолноеИмя;
	
	ЗагрузчикФайла.УстановитьПуть(ПутьКНесколькимФайлам);
	
	ДеревоТестов = ЗагрузчикФайла.Загрузить();
	
	Утверждения.ПроверитьТип(ДеревоТестов, "Массив", "ДеревоТестов");
	Утверждения.ПроверитьРавенство(ДеревоТестов.Количество(), 2, "Узлы верхнего уровня");
	
	Контейнер = ДеревоТестов[0];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами1);
	
	Контейнер = ДеревоТестов[1];
	ПроверитьКонтейнер(Контейнер, ФайлСТестами2);
КонецПроцедуры
// } Loader interface
